#!/usr/bin/env zsh

set -euo pipefail

SCRIPT_DIR=$(cd "$(dirname "$0")" && pwd)
CONFIG_FILE="$SCRIPT_DIR/input/config.json"

: "${OWNER:?Set OWNER (address) globally in env}"
: "${WETH:?Set WETH (address) globally in env}"

CHAIN_ID=${CHAIN_ID:?CHAIN_ID must be set}

CHAIN_CFG=$(jq -e ".\"$CHAIN_ID\"" "$CONFIG_FILE") || { echo "No config found for chain id $CHAIN_ID in $CONFIG_FILE" >&2; exit 1 }
HAS_AGG=$(jq -r ".\"$CHAIN_ID\".aggregator // empty" "$CONFIG_FILE")

AGGREGATOR=""

# Detect broadcast flag in arguments
SHOULD_BROADCAST=0
for arg in "$@"; do
  [[ "$arg" == "--broadcast" ]] && SHOULD_BROADCAST=1
done

persist_aggregator_address() {
  local tmp
  tmp=$(mktemp)
  jq --indent 4 ".\"$CHAIN_ID\".aggregator = \"$AGGREGATOR\"" "$CONFIG_FILE" > "$tmp"
  mv "$tmp" "$CONFIG_FILE"
  echo "Updated aggregator in config for chain $CHAIN_ID"
}

persist_oracle_address() {
  local tmp
  tmp=$(mktemp)
  jq --indent 4 ".\"$CHAIN_ID\".oracle = \"$ORACLE\"" "$CONFIG_FILE" > "$tmp"
  mv "$tmp" "$CONFIG_FILE"
  echo "Updated oracle in config for chain $CHAIN_ID"
}

resolve_script_name() {
  local section="$1" name="$2" dir file
  dir=$([[ "$section" == "wrappers" ]] && echo "wrapper" || echo "adapter")
  file="script/${dir}/Deploy${name}.s.sol"
  [[ -f "$file" ]] || { echo "Expected script file not found: $file" >&2; exit 1; }
  echo "Deploy${name}"
}

deploy_core_stack() {
  echo "Deploying Aggregator: BaseCoinWrapper + MultiWrapper + OffchainOracle (chain $CHAIN_ID)"
  local output json_output
  output=$(forge script DeployAggregator --json "$@") || {
    echo "Core deployment failed" >&2
    exit 1
  }

  # forge may emit warnings and secondary JSON lines; pick the last object that contains `returns`
  json_output=$(jq -sR 'split("\n")
    | map(try fromjson catch empty)
    | map(select(type=="object" and has("returns")))
    | last' <<<"$output")

  [[ -n "$json_output" && "$json_output" != "null" ]] || { echo "Unable to parse forge JSON output" >&2; exit 1; }

  AGGREGATOR=$(jq -er '.returns.aggregator.value // .returns[0].value // .returns.aggregator // .returns[0]' <<<"$json_output") || { echo "Unable to parse OffchainOracle address" >&2; exit 1; }
  echo "OffchainOracle set to $AGGREGATOR"

  [[ "$SHOULD_BROADCAST" -eq 1 ]] && persist_aggregator_address
}

deploy_usd_oracle() {
  local oracle_script=""

  if echo "$CHAIN_CFG" | jq -e '.env.denoms? // empty | length > 0' >/dev/null; then
    oracle_script="DeployUsdOracleSei"
  elif echo "$CHAIN_CFG" | jq -e '.env.feeds? // empty | length > 0' >/dev/null; then
    oracle_script="DeployUsdOracle"
  else
    echo "No USD oracle config found for chain $CHAIN_ID; skipping"
    return
  fi

  echo "Deploying USD oracle ($oracle_script) for chain $CHAIN_ID"
  local output json_output
  output=$(forge script "$oracle_script" --json "$@") || {
    echo "USD oracle deployment failed" >&2
    exit 1
  }

  json_output=$(jq -sR 'split("\n")
    | map(try fromjson catch empty)
    | map(select(type=="object" and has("returns")))
    | last' <<<"$output")

  [[ -n "$json_output" && "$json_output" != "null" ]] || { echo "Unable to parse forge JSON output" >&2; exit 1; }

  ORACLE=$(jq -er '.returns.oracle.value // .returns[0].value // .returns.oracle // .returns[0]' <<<"$json_output") || { echo "Unable to parse USD oracle address" >&2; exit 1; }
  echo "USD oracle set to $ORACLE"

  [[ "$SHOULD_BROADCAST" -eq 1 ]] && persist_oracle_address
}

run_section() {
  local section="$1" # "wrappers" or "adapters"
  shift
  local forge_args=("$@")
  local entries_len
  entries_len=$(echo "$CHAIN_CFG" | jq -r ".${section} // [] | length")

  [[ "$entries_len" -eq 0 ]] && { echo "No ${section} configured for chain $CHAIN_ID"; return; }

  for ((i=0; i<entries_len; i++)); do
    local name script
    name=$(echo "$CHAIN_CFG" | jq -r ".${section}[$i].name")
    script=$(resolve_script_name "$section" "$name")

    echo "Running $section -> $name ($script)"
    if ! env "INDEX=$i" forge script "$script" "${forge_args[@]}"; then
      echo "Error running $section/$name ($script); aborting." >&2
      exit 1
    fi
  done
}

[[ -n "$HAS_AGG" ]] || deploy_core_stack "$@"

[[ -n "$HAS_AGG" ]] || run_section "wrappers" "$@"
[[ -n "$HAS_AGG" ]] || run_section "adapters" "$@"

deploy_usd_oracle "$@"

echo "Done for chain $CHAIN_ID"
